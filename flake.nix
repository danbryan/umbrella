# This flake was initially generated by fh, the CLI for FlakeHub (version 0.1.10)
{

  # Flake inputs
  inputs = {
    flake-compat.url = "https://flakehub.com/f/edolstra/flake-compat/*.tar.gz";

    flake-schemas.url = "https://flakehub.com/f/DeterminateSystems/flake-schemas/*.tar.gz";

    nixpkgs.url = "https://flakehub.com/f/NixOS/nixpkgs/*.tar.gz";

    rust-overlay = {
      url = "github:oxalica/rust-overlay";
      inputs.nixpkgs.follows = "nixpkgs";
    };
  };

  # Flake outputs that other flakes can use
  outputs = { self, flake-compat, flake-schemas, nixpkgs, rust-overlay }:
    let
      # Nixpkgs overlays
      overlays = [
        rust-overlay.overlays.default
        (final: prev: {
          rustToolchain = final.rust-bin.stable.latest.default;
        })
      ];

      # Helpers for producing system-specific outputs
      supportedSystems = [ "x86_64-linux" "aarch64-darwin" "x86_64-darwin" "aarch64-linux" ];
      forEachSupportedSystem = f: nixpkgs.lib.genAttrs supportedSystems (system: f {
        pkgs = import nixpkgs { inherit overlays system; };
      });
    in {
      # Schemas tell Nix about the structure of your flake's outputs
      schemas = flake-schemas.schemas;

      # The package
      packages = forEachSupportedSystem ({ pkgs }: rec {
        default = umbrella;
        umbrella =
          ((let rustVersion = pkgs.rust-bin.stable.latest.default;
            in pkgs.makeRustPlatform {
              cargo = rustVersion;
              rustc = rustVersion;
            }).buildRustPackage {
          pname = "umbrella";
          version = "0.1.0";
          src = ./.;
          cargoSha256 = "";
          cargoLock.lockFile = ./Cargo.lock;
          cargoLock.outputHashes = {
            "decaf377-fmd-0.79.0-alpha.1" = "sha256-sLN6SmM4QyewLLqQhHL3UKV9TuAT1NbtAPkq0/50cLQ=";
            "f4jumble-0.0.0" = "sha256-Zc07fVIPuGVq6gUW1OOehr7HkcCBNUPYNh7POmRECrE=";
          };
        }).overrideAttrs (_: { doCheck = false; }); # Disable tests to improve build times
      });

      # The module
      nixosModules = rec {
        default = umbrella;
        umbrella = { config, lib, pkgs, ... }:
          let
            cfg = config.services.umbrella;
            umbrella = self.outputs.packages.${pkgs.system}.default;
          in {
            options = {
              services.umbrella = with lib; with types; {
                enable = mkEnableOption "Enable Umbrella service to monitor Penumbra validator nodes";
                nodes = mkOption {
                  type = listOf str;
                  description = "List of fullnodes to connect to";
                  default = [];
                };
                fallbacks = mkOption {
                  type = listOf str;
                  description = "List of fallback RPC endpoints to connect to";
                  default = [];
                };
                validators = lib.mkOption {
                  type = listOf str;
                  description = "List of validator identity keys to monitor";
                  default = [];
                };
                bind = lib.mkOption {
                  type = nullOr str;
                  description = "Address to bind the /metrics endpoint to, default if unspecified";
                };
                pollInterval = mkOption {
                  type = nullOr str;
                  description = "Minimum interval at which the exporter polls the RPC endpoints, default if unspecified";
                };
                connectTimeout = mkOption {
                  type = nullOr str;
                  description = "Timeout for connecting to the RPC endpoints, default if unspecified";
                };
              };
            };

            config = with lib; mkIf config.services.umbrella.enable {
              environment.systemPackages = [ umbrella ];

              systemd.services.umbrella = {
                description = "Umbrella service to monitor Penumbra validator nodes";
                wantedBy = [ "multi-user.target" ];
                serviceConfig = {
                  ExecStart = ''
                    ${umbrella}/bin/umbrella
                    ${concatStringsSep " " (map (n: "--node ${n}") cfg.nodes)}
                    ${concatStringsSep " " (map (f: "--fallback ${f}") cfg.fallbacks)}
                    ${concatStringsSep " " (map (v: "--validator ${v}") cfg.validators)}
                    ${optionalString (cfg.bind != null) (b: "--bind ${b}")}
                    ${optionalString (cfg.pollInterval != null) (p: "--poll-interval ${p}")}
                    ${optionalString (cfg.connectTimeout != null) (c: "--connect-timeout ${c}")}
                  '';
                  Restart = "always";
                  DynamicUser = "yes";
                  RestartSec = "5";
                };
              };
            };
          };
        };

      # Development environments
      devShells = forEachSupportedSystem ({ pkgs }: {
        default = pkgs.mkShell {
          # Pinned packages available in the environment
          packages = with pkgs; [
            rustToolchain
            cargo-bloat
            cargo-edit
            cargo-outdated
            cargo-udeps
            cargo-watch
          ];

          # Environment variables
          env = {
            RUST_BACKTRACE = "1";
          };
        };
      });
    };
}
